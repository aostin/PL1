package compiler.syntax;

// Declaraci�n de importaciones 
//(No modificar las proporcionadas. Se pueden agregar mas)

import java_cup.runtime.Symbol;
import java.util.*;

import es.uned.lsi.compiler.lexical.*;
import es.uned.lsi.compiler.code.*;
import es.uned.lsi.compiler.intermediate.*;
import es.uned.lsi.compiler.semantic.*;
import es.uned.lsi.compiler.semantic.symbol.*;
import es.uned.lsi.compiler.semantic.type.*;
import es.uned.lsi.compiler.syntax.*;

import compiler.CompilerContext;
import compiler.lexical.*;
import compiler.syntax.nonTerminal.*;
import compiler.semantic.*;
import compiler.semantic.symbol.*;
import compiler.semantic.type.*;
import compiler.intermediate.*;
import compiler.code.*;

// Declaraci�n del c�digo de usuario

action code {:
	
	SyntaxErrorManager   syntaxErrorManager   = CompilerContext.getSyntaxErrorManager();
	SemanticErrorManager semanticErrorManager = CompilerContext.getSemanticErrorManager ();
	ScopeManagerIF       scopeManager         = CompilerContext.getScopeManager ();
	FinalCodeFactoryIF   finalCodeFactory     = CompilerContext.getFinalCodeFactory ();

:}	

parser code {:
	SyntaxErrorManager syntaxErrorManager = CompilerContext.getSyntaxErrorManager();
	
	public void syntax_error(Symbol symbol)
	{ 
	    Token token = (Token) symbol.value;
	    syntaxErrorManager.syntaxError ("Error sintactico", token);	    
	}
		
	public void unrecovered_syntax_error(java_cup.runtime.Symbol symbol)
	{	
	    Token token = (Token) symbol.value;
	    syntaxErrorManager.syntaxFatalError ("Error fatal", token);
	}
:}

// Declaraci�n de terminales (Ejemplo)

terminal Token MENOS;
terminal Token POR;
terminal Token MAYOR;
terminal Token IGUAL;
terminal Token AND;
terminal Token ARRAY;
terminal Token BEGIN;
terminal Token BOOLEAN;
terminal Token CONST;
terminal Token DO;
terminal Token ELSE;
terminal Token END;
terminal Token FALSE;
terminal Token FOR;
terminal Token IF;
terminal Token INTEGER;
terminal Token MODULE;
terminal Token NOT;
terminal Token OF;
terminal Token OR;
terminal Token PROCEDURE;
terminal Token RECORD;
terminal Token RETURN;
terminal Token THEN;
terminal Token TO;
terminal Token TRUE;
terminal Token TYPE;
terminal Token VAR;
terminal Token WHILE;
terminal Token WRITESTRING;
terminal Token WRITEINT;
terminal Token WRITELN;
terminal Token ID;
terminal Token CDIGITO;
terminal Token CCADENA;
terminal Token APAREN;
terminal Token CPAREN;
terminal Token ACOR;
terminal Token CCOR;
terminal Token COMA;
terminal Token PCOMA;
terminal Token DOSPUNTOS;
terminal Token PUNTOS;
terminal Token PUNTO;
// ...


// Declaraci�n de no terminales
// no modificar los propuestos

non terminal  			program;
non terminal Axiom		axiom;
non terminal 			principio;
non terminal			medio;
non terminal 			seccionConstantes;
non terminal			seccionTipos;
non terminal			seccionVariables;
non terminal			seccionSubprogramas;
non terminal			listaDeclaraciones;
non terminal			declaracionConstantes;
non terminal			declaracionConstante;
non terminal			declaracionTipos;
non terminal			declaracionTipo;
non terminal			declaracionVariable;
non terminal			declaracionVariables;
non terminal			valorConstante;
non terminal			tipoPrimitivo;
non terminal			tipoDato;
non terminal			expresion;
non terminal 			operador;
non terminal 			valorExpresion;
non terminal			expresionCompuesta;
non terminal			parentesis;
non terminal			corchetes;
non terminal			accesoVector;
non terminal			llamadaFuncion;
non terminal			listaValores;
non terminal			sentencias;
non terminal			sentenciaAsignacion;
non terminal			sentenciasIF;
non terminal			sentenciasFOR;
non terminal			cuerpo;
non terminal			sentenciasWrite;
non terminal			writeString;
non terminal			writeInt;
non terminal			llamadaProcedimiento;
non terminal			procedimiento;
non terminal			funcion;
non terminal			subprograma;
non terminal			cuerpoSubprograma;
non terminal			sentenciasSubprograma;
non terminal			sentenciasIFSubprograma;
non terminal			sentenciasFORSubprograma;
non terminal			valorSentencia;
non terminal			valorSimple;
non terminal			tipoSentencia;
non terminal 			tipoSentenciaSubprograma;
// ...


// Declaraci�n de relaciones de precedencia
precedence left PUNTO, APAREN, CPAREN, ACOR, CCOR;
precedence right NOT;
precedence left POR, AND;
precedence left MENOS, OR;
precedence left MAYOR, IGUAL;

// ...

// Declaraci�n de reglas de producci�n


start with program;

program ::= 
  {: 
        syntaxErrorManager.syntaxInfo ("Starting parsing..."); 
   :}
  axiom:ax
  {: 
  		// No modificar esta estructura, aunque se pueden a�adir m�s acciones sem�nticas
  		// Para la entrega de febrero pueden comentarse las dos sentencias siguientes:
  		//List intermediateCode = ax.getIntermediateCode ();
  		//finalCodeFactory.create (intermediateCode);
  		// En caso de no comentarse las sentencias anteriores puede generar una excepcion
  		// en las llamadas a cupTest en la entrega de Febrero. Esto es debido a que 
  		// a�n no se tendr� implementada la generaci�n de c�digo intermedio ni final.
  		// Para la entrega de Junio deber�n descomentarse y usarse.
  		
  		syntaxErrorManager.syntaxInfo ("Parsing process ended.");
   :};

axiom ::= principio medio ;

principio ::= MODULE ID PCOMA ;

medio ::=  listaDeclaraciones seccionSubprogramas cuerpo | listaDeclaraciones cuerpo | seccionSubprogramas cuerpo | cuerpo;

//Esta parte la hacemos distinta del cuerpo del programa principal con el fin de que se pueda incluir un RETURN dentro de un IF o un FOR en el subprograma

subprograma::= listaDeclaraciones seccionSubprogramas cuerpoSubprograma |  listaDeclaraciones cuerpoSubprograma | seccionSubprogramas cuerpoSubprograma | cuerpoSubprograma;

seccionSubprogramas::=  funcion | procedimiento | funcion seccionSubprogramas | procedimiento seccionSubprogramas;

procedimiento::= PROCEDURE ID PCOMA subprograma | PROCEDURE ID APAREN CPAREN PCOMA subprograma | PROCEDURE ID APAREN declaracionVariables CPAREN PCOMA subprograma; 
funcion::=  PROCEDURE ID DOSPUNTOS tipoPrimitivo PCOMA subprograma | PROCEDURE ID APAREN CPAREN DOSPUNTOS tipoPrimitivo PCOMA subprograma | PROCEDURE ID APAREN declaracionVariables CPAREN DOSPUNTOS tipoPrimitivo PCOMA subprograma; 
cuerpoSubprograma::=BEGIN sentenciasSubprograma END PCOMA;

sentenciasSubprograma::= tipoSentenciaSubprograma PCOMA | tipoSentenciaSubprograma PCOMA sentenciasSubprograma ;
tipoSentenciaSubprograma::=sentenciaAsignacion | sentenciasIFSubprograma | sentenciasFORSubprograma | sentenciasWrite | llamadaProcedimiento | RETURN valorSimple;


sentenciasIFSubprograma::= IF expresionCompuesta THEN sentenciasSubprograma ELSE sentenciasSubprograma END 
				| IF expresionCompuesta THEN sentenciasSubprograma END ;
				
sentenciasFORSubprograma::= FOR sentenciaAsignacion TO ID DO sentenciasSubprograma END | FOR sentenciaAsignacion TO CDIGITO DO sentenciasSubprograma END;


listaDeclaraciones ::= seccionConstantes | seccionTipos | seccionConstantes seccionTipos | seccionVariables | seccionConstantes seccionVariables | seccionTipos seccionVariables | seccionConstantes seccionTipos seccionVariables;

seccionConstantes ::= CONST declaracionConstantes;

declaracionConstantes ::= declaracionConstante declaracionConstantes| declaracionConstante ;

declaracionConstante ::= ID IGUAL valorConstante PCOMA;

valorConstante ::= TRUE | FALSE | CDIGITO;
valorSimple::= valorConstante  | ID;

seccionTipos ::= TYPE declaracionTipos;

declaracionTipos ::= declaracionTipo | declaracionTipo declaracionTipos;

declaracionTipo ::= ID IGUAL ARRAY ACOR CDIGITO PUNTOS CDIGITO CCOR OF tipoPrimitivo PCOMA;

tipoPrimitivo ::= INTEGER | BOOLEAN;


seccionVariables ::= VAR declaracionVariables;

declaracionVariables ::= declaracionVariable PCOMA | declaracionVariable PCOMA declaracionVariables;
declaracionVariable ::= ID DOSPUNTOS tipoDato | ID COMA declaracionVariable;
tipoDato::= ID | tipoPrimitivo;


// expresiones y aceso a vectores. Se contempla la posibilidad de corchetes y parentesis anidados

expresionCompuesta::= expresion operador expresion | APAREN expresion operador expresion CPAREN | ACOR expresion operador CCOR;
expresion::= valorExpresion | expresionCompuesta | parentesis | corchetes;
parentesis::= APAREN parentesis expresion CPAREN | APAREN expresion CPAREN;
corchetes::= ACOR corchetes expresion CCOR | ACOR expresion CCOR;
operador ::= POR | AND | MENOS | OR | MAYOR | IGUAL;
valorExpresion ::= valorSimple | accesoVector;
accesoVector::= ID ACOR CDIGITO CCOR | ID ACOR ID CCOR;

//Llamadas a funciones y procedimientos

listaValores::= valorExpresion | valorExpresion COMA listaValores | llamadaFuncion ;

llamadaFuncion::= ID APAREN listaValores CPAREN | ID APAREN llamadaFuncion COMA listaValores CPAREN;

llamadaProcedimiento::= ID| ID APAREN CPAREN| ID APAREN listaValores CPAREN ;

//Cuerpo del programa
cuerpo::= BEGIN sentencias END PCOMA;

//Sentencias tipo 

sentencias::= tipoSentencia PCOMA | tipoSentencia PCOMA sentencias;

tipoSentencia::= sentenciaAsignacion | sentenciasIF | sentenciasFOR | sentenciasWrite | llamadaProcedimiento;

sentenciaAsignacion::= ID DOSPUNTOS IGUAL valorSentencia | accesoVector DOSPUNTOS IGUAL valorSentencia;

valorSentencia::= expresionCompuesta | llamadaFuncion | valorSimple ;

sentenciasIF::= IF expresionCompuesta THEN sentencias ELSE sentencias END | IF expresionCompuesta THEN sentencias END ;

//Vamos a considerar que la expresion de comienzo de un for es cualquier sentecia de asignación de las antes expuestas

sentenciasFOR::= FOR sentenciaAsignacion TO ID DO sentencias END | FOR sentenciaAsignacion TO CDIGITO DO sentencias END;

sentenciasWrite::= writeString | writeInt | WRITELN;

writeString::= WRITESTRING APAREN CCADENA CPAREN;
writeInt::= WRITEINT APAREN ID CPAREN | WRITEINT APAREN CDIGITO CPAREN;


//Tratamiento de errores

//Errores al comienzo del programa

principio::= error {: 
        syntaxErrorManager.syntaxInfo ("Se esperaba la palabra reservada MODULE"); 
   :} ID PCOMA
   		| MODULE ID error  {:syntaxErrorManager.syntaxInfo ("Se esperaba el delimitador ';'"); 
   :} ;

  
sentencias::= tipoSentencia error {:syntaxErrorManager.syntaxInfo ("Error, se espera el simbolo ';'"); 
   :}  |
   	tipoSentencia error {:syntaxErrorManager.syntaxInfo ("Error, se espera el simbolo ';'"); 
   :} sentencias ;


/*sentenciaAsignacion::= error  {:syntaxErrorManager.syntaxInfo ("Error en el identificador de la sentencia"); 
   :} DOSPUNTOS IGUAL valorSentencia;
*/

/*   		
//Errores en sentencias de asignacion  		


//Errores en procedimientos
procedimiento::= error {:syntaxErrorManager.syntaxInfo ("Error en la definicion del procedimiento"); 
   :} ;
   
//Errores en sentenciasIF

sentenciasIF::= error {:syntaxErrorManager.syntaxInfo ("Error en la construccion de la sentencia IF"); 
   :} expresionCompuesta THEN
   | IF sentencias THEN error {:syntaxErrorManager.syntaxInfo ("Error en la construccion de la sentencia IF"); 
   :}   ;

//Error en sentencias FOR
*/


